/* This is meant to be used with */
/* https://www.bottlecaps.de/rr/ui */
/* to visualize the command syntax */

/* Just the basic user characters */

CHAR       ::= [a-zA-Z]
TERMINATOR ::= 'NEWLINE'| ';'
INT        ::= [0-9]+
MATHOP     ::= '+' | '-' | '*' | '/'
LOGICOP    ::= '||' | '&&' | '!'
COMPARATOR ::= '==' | '!=' | '>' | '>=' | '<' | '<='
WORD       ::= CHAR+
SPACE      ::= ' '
PRINTABLE  ::= CHAR | INT | SPACE | PUNCTUATION

/* objects which exist */

NAME ::= WORD+

/* Clauses that are used in multiple player commands */

DIRECTION ::= ((n | ne | e | se | s | sw | w | nw | u | d | i | o) | (north | northeast | east | southeast | south | southwest | west | northwest | up | down| in | out))
ITEM ::= NAME /* alias with intent */

/* Actual player commands */

DropExpression      ::= ('d' | 'drop') ITEM
GetExpression       ::= ('g' | 'get')  ITEM
HelpExpression      ::= 'help'
InventoryExpression ::= ('i' | 'inventory')
LockExpression      ::= 'lock' DIRECTION ('with' ITEM)?
LookExpression      ::= ('l' | 'look')
MoveExpression      ::= ('go' DIRECTION) | DIRECTION
UnlockExpression    ::= 'unlock' DIRECTION ('with' ITEM)?

/* Debug commands */
EchoExpression      ::= 'echo' WORD+
TeleportExpression  ::= 'teleport' INT

/* Interpreter commands, non-player */
PrintExpression ::= 'print' PRINTABLE+
CommentExpression ::= '#' ((ANY)?)* 'NEWLINE'

/* And bringing it all together */
PlayerExpression ::=        (
                                DropExpression      |
                                GetExpression       |
                                HelpExpression      |
                                InventoryExpression |
                                LockExpression      |
                                LookExpression      |
                                MoveExpression      |
                                UnlockExpression
                            )

DebugExpression ::=         (
                                EchoExpression      |
                                TeleportExpression
                            )

InterpreterExpression::=    (
                                PrintExpression |
                                CommentExpression
                            )

STATEMENT ::= (PlayerExpression | DebugExpression | InterpreterExpression) TERMINATOR
BODY      ::= STATEMENT+

/* Meta-level expressions are a bit different */
ComparisonClause  ::= 'VALUE' COMPARATOR 'VALUE'
BooleanClause     ::= 'BOOLEAN' ((LOGICOP 'BOOLEAN')?)*
OptionClause      ::= '[option' PRINTABLE+ ']'
ChoiceClause      ::= '[choice]' 'NEWLINE' (OptionClause 'NEWLINE' BODY)+ 'NEWLINE' '[end]'
IfElseClause      ::= '[if' BooleanClause ']' 'NEWLINE' BODY 'NEWLINE' (('[elif' BooleanClause ']' 'NEWLINE' BODY 'NEWLINE')?)+ ('else' 'NEWLINE' BODY 'NEWLINE')? '[end]'
TextEscapeClause  ::= '[' ( 'lbrack' | 'rbrack' | 'numbersign' ) ']'